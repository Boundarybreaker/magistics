import de.undercouch.gradle.tasks.download.Download

buildscript {
	repositories {
		jcenter()
		maven {
			name 'forge'
			url 'http://files.minecraftforge.net/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id 'org.ajoberstar.grgit' version '2.1.0'
	id 'de.undercouch.download' version '3.3.0'
}

repositories {
	mavenCentral()
	maven {
		name 'Mobius Repo'
		url 'http://tehnut.info/maven'
	}
	maven {
		name 'JEI Repo'
		url 'http://modmaven.k-4u.nl/'
	}
	maven {
		name 'Progwm16 Repo'
		url 'http://dvs1.progwml6.com/files/maven'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

task wrapper(type: Wrapper) {
	gradleVersion = gradle_version
}

task setupWorkspace(type: Download, dependsOn: setupDecompWorkspace) {
	def forgeArchive = project.file('forge.zip')
	
	src 'http://files.minecraftforge.net/maven/net/minecraftforge/forge/' + forge_version + '/forge-' + forge_version + '-mdk.zip'
	dest forgeArchive
	
	doLast {
		project.copy {
			from zipTree(forgeArchive)
			into 'forge'
		}
		
		project.file('forge/eclipse').renameTo(project.file('eclipse'))
		
		project.file('forge.zip').delete()
		project.file('forge').deleteDir()
	}
}

version = grgit.describe(longDescr: true)
group = mod_group
archivesBaseName = mod_id

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileApiJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
	version = forge_version
	mappings = forge_mappings
	runDir = 'run'
	useDepAts = true
	makeObfSourceJar = false
	
	replaceIn 'Magistics.java'
	replace "$version", project.version
}

dependencies {
	deobfCompile 'mezz.jei:jei_1.11.2:4.5.0.287'
	deobfCompile 'mcp.mobius.waila:Hwyla:1.8.17-B31_1.11'
	deobfCompile 'cpw.mods:ironchest:1.11.2-7.0.21.806'
}

processResources {
	exclude '**/*.db'
	exclude '**/*.DS_Store'
}