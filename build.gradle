buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'ChickenBones Repo'
        url = 'http://chickenbones.net/maven'
    }
    maven {
        name = 'IC2 Repo'
        url = 'http://maven.ic2.player.to/'
    }
    maven {
        name 'Mobius Repo'
        url 'http://mobiusstrip.eu/maven'
    }
}

apply plugin: 'forge'

version = project_version
group = 'T145.magistics'
archivesBaseName = 'Magistics'

minecraft {
    version = forge_version
    runDir = 'eclipse'
}

dependencies {
    compile 'appeng:appliedenergistics2:rv1-stable-1:dev'
    compile 'com.mod-buildcraft:buildcraft:6.3.6:dev'
    compile 'com.pahimar.ee3:EquivalentExchange3:1.7.10-0.2.374:dev'
    compile 'cpw.mods:ironchest:1.7.10-6.0.62.742:deobf'

    compile 'codechicken:CodeChickenLib:1.7.10-1.1.1.110:dev'
    compile 'codechicken:ForgeMultipart:1.7.10-1.1.1.320:dev'

    compile 'codechicken:CodeChickenCore:1.7.10-1.0.4.35:dev'
    compile 'codechicken:EnderStorage:1.7.10-1.4.5.27:dev'
    compile 'codechicken:NotEnoughItems:1.7.10-1.0.4.83:dev'

    compile fileTree(dir: 'lib', include: '*.jar')
}

processResources {
    // this will ensure that this task is redone when the version changes
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // copy the mcmod.info
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

String release_title = project.minecraft.version + '-' + project.version

// modifies the actual release
jar {
    version = release_title
}

// add a developer release
task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'dev'
	version = release_title
}

// add a javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
	from 'build/docs/javadoc'
	classifier = 'javadoc'
	version = release_title
}

artifacts {
	archives deobfJar, javadocJar
}