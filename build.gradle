buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
}

apply plugin: 'forge'

version = project_version
group = 'T145.magistics'
archivesBaseName = 'Magistics'

minecraft {
    version = forge_version
    runDir = 'eclipse/assets'
}

dependencies {
    compile 'mcp.mobius.waila:Waila:1.5.5_1.7.10'
    compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'
    compile 'cpw.mods:ironchest:1.7.10-6.0.62.742:deobf'
    compile 'codechicken:CodeChickenLib:1.7.10-1.1.1.104:dev'
    compile 'codechicken:ForgeMultipart:1.7.10-1.1.0.310:dev'
    compile 'codechicken:EnderStorage:1.7.10-1.4.5.24:dev'

    compile fileTree(dir: 'eclipse/mods', include: '*.jar')
    compile fileTree(dir: 'lib', include: '*.jar')
}

processResources {
    // this will ensure that this task is redone when the version changes
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // copy the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task copyChicken(type: Copy, dependsOn: "extractUserDev") {
    from {
        configurations.compile
    }
    include '**/*Chicken*.jar', '**/*NotEnoughItems*.jar', '**/*Multipart*.jar'
    into file(System.getProperty('user.dir') + '/eclipse/mods')
    mustRunAfter 'deobfBinJar'
    mustRunAfter 'repackMinecraft'
}
tasks.setupDevWorkspace.dependsOn copyChicken
tasks.setupDecompWorkspace.dependsOn copyChicken

// modifies the actual release
jar {
	version = "${project.minecraft.version}-${project.version}"
}

// add a developer release
task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'dev'
	version = "${project.minecraft.version}-${project.version}"
}

// add a javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
	from 'build/docs/javadoc'
	classifier = 'javadoc'
	version = "${project.minecraft.version}-${project.version}"
}

artifacts {
	archives deobfJar, javadocJar
}